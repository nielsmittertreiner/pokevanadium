const LOCALID_FEEBAS_SELLER = 4
const LOCALID_ARTIST = 9
const LOCALID_OLD_WOMAN = 12
const LOCALID_OLD_MAN = 15
const LOCALID_AIRLOCK_GRUNT_1 = 18
const LOCALID_AIRLOCK_GRUNT_2 = 19
const LOCALID_FISHERMAN = 20

const LOCALID_BOAT_MURENA = 10

const LOCALID_BOAT_MURENA_SHIPYARD = 1
const LOCALID_FISHERMAN_MURENA_SHIPYARD = 2

const LOCALID_BOAT_LITOR = 1
const LOCALID_FISHERMAN_LITOR = 2


mapscripts MurenaCity_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_FISHERMAN_FERRY_STATE, 1: MurenaCity_EventScript_SetUpSailing
    ]
    MAP_SCRIPT_ON_FRAME_TABLE [
         VAR_FISHERMAN_FERRY_STATE, 2: MurenaCity_EventScript_SailToLitor
    ]

    MAP_SCRIPT_ON_TRANSITION {
        if (var(VAR_MURENA_CITY_STATE) < 1) {
            setobjectxyperm(LOCALID_OLD_WOMAN, 41, 20)
        }
        setrespawn(HEAL_LOCATION_MURENA_CITY)
        setflag(FLAG_VISITED_MURENA_CITY)
        call(FishermanFerry_EventScript_UpdateFerryLocation)
    }
}

script MurenaCity_EventScript_SetUpSailing {
    setvar(VAR_FISHERMAN_FERRY_STATE, 2)
    setflag(FLAG_DONT_TRANSITION_MUSIC)
    hideobjectat(OBJ_EVENT_ID_PLAYER, MAP_MURENA_CITY)
}

script MurenaCity_EventScript_SailToLitor {
    setvar(VAR_FISHERMAN_FERRY_STATE, 0)
    playbgm(MUS_SAILING, 0)
    applymovement(OBJ_EVENT_ID_PLAYER, MurenaCity_Movement_SailToLitor)
    applymovement(LOCALID_BOAT_MURENA, MurenaCity_Movement_SailToLitor)
    waitmovement(0)
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    fadedefaultbgm
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
    waitmovement(0)
    showobjectat(OBJ_EVENT_ID_PLAYER, MAP_LITOR_TOWN)
    applymovement(OBJ_EVENT_ID_PLAYER, MurenaCity_Movement_ExitBoatLitorTown)
    waitmovement(0)
    setobjectxyperm(LOCALID_FISHERMAN_LITOR, 34, 22)
    addobject(LOCALID_FISHERMAN_LITOR)
    setobjectpriority(LOCALID_FISHERMAN_LITOR, MAP_LITOR_TOWN, 0)
    applymovement(LOCALID_FISHERMAN_LITOR, Common_Movement_WalkLeft)
    waitmovement(0)
    clearflag(FLAG_HIDE_LITOR_TOWN_FISHERMAN)
    addobject(LOCALID_BOAT_LITOR)
    setobjectpriority(LOCALID_BOAT_LITOR, MAP_LITOR_TOWN, 4)
    clearflag(FLAG_HIDE_LITOR_TOWN_BOAT)
    setflag(FLAG_HIDE_MURENA_CITY_SHIPYARD_BOAT)
    setflag(FLAG_HIDE_MURENA_CITY_BOAT)
    hideobjectat(LOCALID_BOAT_MURENA, MAP_MURENA_CITY)
    hideobjectat(LOCALID_BOAT_MURENA_SHIPYARD, MAP_MURENA_CITY_SHIPYARD)
    load_speech_bubble(LOCALID_FISHERMAN_LITOR)
    msgbox("Ahoy! We've arrived at LITOR TOWN!\n"
           "You can smell the salty sea here too.")
    destroy_speech_bubble
    closemessage
    copyvar(VAR_FISHERMAN_FERRY_LOCATION, VAR_0x8008)
    resetobjectpriority(OBJ_EVENT_ID_PLAYER, MAP_LITOR_TOWN)
    resetobjectpriority(LOCALID_FISHERMAN_LITOR, MAP_LITOR_TOWN)
    copyobjectxytoperm(LOCALID_FISHERMAN_LITOR)
    release
    end
}

script MurenaCity_EventScript_TeamAirlockEncounter {
    playbgm(MUS_ENCOUNTER_AQUA, 0x00)
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceLeft)
    waitmovement(0)
    load_speech_bubble(LOCALID_FISHERMAN)
    msgbox("Hey!\n"
           "Let me through!\p"
           "I've got to get an important TENTACOOL\n"
           "haul for the MARKET tomorrow!")
    destroy_speech_bubble
    closemessage
    load_speech_bubble(LOCALID_AIRLOCK_GRUNT_1)
    msgbox("Do not resist!\p"
           "We are impounding your boat for\n"
           "the greater good!")
    destroy_speech_bubble
    closemessage
    applymovement(LOCALID_AIRLOCK_GRUNT_2, MurenaCity_Movement_Grunt2PunchFisherman)
    waitmovement(0)
    playse(SE_M_COMET_PUNCH)
    waitse
    applymovement(LOCALID_FISHERMAN, MurenaCity_Movement_FishermanStrugglesBack)
    waitmovement(0)
    delay(24)
    playse(SE_PIN)
    applymovement(LOCALID_FISHERMAN, Common_Movement_ExclamationMark)
    waitmovement(0)
    getplayerxy(VAR_TEMP_0, VAR_TEMP_1)
    if (var(VAR_TEMP_1) == 28) {
        applymovement(LOCALID_FISHERMAN, MurenaCity_Movement_FishermanRunToPlayer1)
        waitmovement(0)
    } elif (var(VAR_TEMP_1) == 29) {
        applymovement(LOCALID_FISHERMAN, MurenaCity_Movement_FishermanRunToPlayer2)
        waitmovement(0)
    }
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
    waitmovement(0)
    load_speech_bubble(LOCALID_FISHERMAN)
    msgbox("Hey kiddo, ya gotta help me out here!\p"
           "Those thugs are trying to\n"
           "steal my ship!\p"
           "Please do something!")
    destroy_speech_bubble
    closemessage
    if (var(VAR_TEMP_1) == 28) {
        applymovement(OBJ_EVENT_ID_PLAYER, MurenaCity_Movement_PlayerWalkToGrunt)
        waitmovement(0)
    } elif (var(VAR_TEMP_1) == 29) {
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
        waitmovement(0)
    }
    applymovement(LOCALID_AIRLOCK_GRUNT_1, Common_Movement_FaceRight)
    waitmovement(0)
    load_speech_bubble(LOCALID_AIRLOCK_GRUNT_1)
    msgbox("So what are you gonna do?!\n"
           "Save him?\p"
           "Hahaha!\n"
           "I don't think so kid!\p"
           "You can't take the wind\n"
           "out of our sails!")
    destroy_speech_bubble
    closemessage
    trainerbattle_no_intro(TRAINER_MURENA_CITY_GRUNT, MurenaCity_Text_GruntDefeat)
    load_speech_bubble(LOCALID_AIRLOCK_GRUNT_1)
    msgbox("Oh no!\n"
           "You're stronger than I thought!\p"
           "Quickly!\n"
           "Let's get out of here!")
    destroy_speech_bubble
    closemessage
    fadedefaultbgm
    applymovement(LOCALID_AIRLOCK_GRUNT_1, MurenaCity_Movement_Grunt1RunAway)
    applymovement(LOCALID_AIRLOCK_GRUNT_2, MurenaCity_Movement_Grunt2RunAway)
    waitmovement(0)
    removeobject(LOCALID_AIRLOCK_GRUNT_1)
    removeobject(LOCALID_AIRLOCK_GRUNT_2)
    if (var(VAR_TEMP_1) == 28) {
        applymovement(LOCALID_FISHERMAN, MurenaCity_Movement_FishermanWalkToPlayer1)
        waitmovement(0)
    } elif (var(VAR_TEMP_1) == 29) {
        applymovement(LOCALID_FISHERMAN, MurenaCity_Movement_FishermanWalkToPlayer2)
        waitmovement(0)
    }
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceUp)
    waitmovement(0)
    load_speech_bubble(LOCALID_FISHERMAN)
    msgbox("Phew… Thank you.\p"
           "I don't know what I would've done\n"
           "without you…\p"
           "Y'know what, as a reward, I'll ferry you\n"
           "across the straits free of charge!\p"
           "I'll be right here in the SHIPYARD if\n"
           "you need me.")
    destroy_speech_bubble
    closemessage
    applymovement(LOCALID_FISHERMAN, MurenaCity_Movement_FishermanWalkToShipyard)
    waitmovement(0)
    playse(SE_EXIT)
    removeobject(LOCALID_FISHERMAN)
    setvar(VAR_MURENA_CITY_STATE, 1)
    end
}

script MurenaCity_EventScript_ArtistTrader {
    lock
    faceplayer
    if (flag(FLAG_MURENA_NPC_TRADE_COMPLETED) == TRUE) {
        msgbox("Jusqu'ici I am able to understand\n"
               "the ocean blue.", MSGBOX_NPC)
        end
    }
    setvar(VAR_0x8008, INGAME_TRADE_SMEARGLE)
    copyvar(VAR_0x8004, VAR_0x8008)
    specialvar(VAR_RESULT, GetInGameTradeSpeciesInfo)
    copyvar(VAR_0x8009, VAR_RESULT)
    load_speech_bubble(LOCALID_ARTIST)
    msgbox("The ocean is a canvas…\p"
           "But to truly blend into it,\n"
           "that deep dark blue…\p"
           "Only one POKéMON knows the art…\n"
           "Can you bring me one.\p"
           "If you can find me a KECLEON,\n"
           "I will give you my SMEARGLE…", MSGBOX_YESNO)
    destroy_speech_bubble
    closemessage
    if (var(VAR_RESULT) == NO) {
        goto(MurenaCity_EventScript_ArtistTrader_Decline)
    }
    special(ChoosePartyMon)
    waitstate
    copyvar(VAR_0x800A, VAR_0x8004)
    if (var(VAR_0x8004) == 255) {
        goto(MurenaCity_EventScript_ArtistTrader_Decline)
    }
    copyvar(VAR_0x8005, VAR_0x800A)
    specialvar(VAR_RESULT, GetTradeSpecies)
    copyvar(VAR_0x800B, VAR_RESULT)
    compare(VAR_RESULT, VAR_0x8009)
    goto_if_ne(MurenaCity_EventScript_ArtistTrader_NotRequestedMon)
    copyvar(VAR_0x8004, VAR_0x8008)
    copyvar(VAR_0x8005, VAR_0x800A)
    special(CreateInGameTradePokemon)
    special(DoInGameTradeScene)
    waitstate
    bufferspeciesname(0, VAR_0x8009)
    load_speech_bubble(LOCALID_ARTIST)
    msgbox("Magnificent…\n"
           "Such color I have never seen.")
    destroy_speech_bubble
    closemessage
    setflag(FLAG_MURENA_NPC_TRADE_COMPLETED)
    release
    end
}

script MurenaCity_EventScript_ArtistTrader_Decline {
    load_speech_bubble(LOCALID_ARTIST)
    msgbox("I see…\p"
           "I guess true culture is hard to\n"
           "come by in these parts…")
    destroy_speech_bubble
    closemessage
    release
    end
}

script MurenaCity_EventScript_ArtistTrader_NotRequestedMon {
    msgbox("What?! I was offering you a\n"
           "SMEARGLE, not a pair of glasses!", MSGBOX_NPC)
    end
}

script MurenaCity_EventScript_FeebasSeller {
    lock
    faceplayer
    if (flag(FLAG_MURENA_BOUGHT_FEEBAS)) {
        load_speech_bubble(LOCALID_FEEBAS_SELLER)
        msgbox("SORRY KIDDO, I AIN'T GOT\n"
               "ANY MORE FISHES!")
        destroy_speech_bubble
        closemessage
        release
        end
    }
    load_speech_bubble(LOCALID_FEEBAS_SELLER)
    msgbox("HEY KID!\n"
           "WANNA BUY THIS MAGIKARP?!\p"
           "IT'S KINDA UGLY, SO IT'S\n"
           "ONLY ¥500!")
    destroy_speech_bubble
    closemessage
    showmoneybox(0, 0, 0)
    load_speech_bubble(LOCALID_FEEBAS_SELLER)
    msgbox("WHADDYA SAY?\n"
           "¥500 AIN'T MUCH!", MSGBOX_YESNO)
    destroy_speech_bubble
    closemessage
    if (var(VAR_RESULT) == YES) {
        checkmoney(500, 0)
        if (var(VAR_RESULT) == FALSE) {
            load_speech_bubble(LOCALID_FEEBAS_SELLER)
            msgbox("NO NO NO, THAT WON'T DO!")
            destroy_speech_bubble
            closemessage
            release
            end
        }
        delay(10)
        playse(SE_SHOP)
        removemoney(500, 0)
        updatemoneybox(0, 0)
        delay(30)
        hidemoneybox
        givemon(SPECIES_FEEBAS, 5, ITEM_NONE)
        load_speech_bubble(LOCALID_FEEBAS_SELLER)
        msgbox("HERE YA GO KIDDO!")
        setflag(FLAG_MURENA_BOUGHT_FEEBAS)
        destroy_speech_bubble
        closemessage
    } else {
        hidemoneybox
        load_speech_bubble(LOCALID_FEEBAS_SELLER)
        msgbox("IF YA DON'T APPRECIATE THE UGLY,\n"
               "YA DON'T DESERVE THE BEAUTIFUL!")
        destroy_speech_bubble
        closemessage
    }
    release
    end
}

script MurenaCity_EventScript_OldMan {
    lock
    faceplayer
    if (flag(FLAG_MURENA_SHOWN_RELICANTH)) {
        load_speech_bubble(LOCALID_OLD_MAN)
        msgbox("Thank you friend, for showing me\n"
               "the true beauty of a RELICANTH.\p"
               "Perhaps once I will catch one of\n"
               "my very own…")
        destroy_speech_bubble
        closemessage
        release
        end
    }
    load_speech_bubble(LOCALID_OLD_MAN)
    msgbox("I am looking for a creature…\n"
           "Ancient in age.\l"
           "Aquatic in nature.\p"
           "Bring my aquatic anomaly…\n"
           "For you shall be rewarded handsomely.")
    destroy_speech_bubble
    closemessage
    delay(30)
    specialvar(VAR_RESULT, CheckMurenaCityRelicanth)
    if (var(VAR_RESULT) == TRUE) {
        playse(SE_PIN)
        applymovement(LOCALID_OLD_MAN, Common_Movement_ExclamationMark)
        waitmovement(0)
        load_speech_bubble(LOCALID_OLD_MAN)
        msgbox("Oh yes…\n"
               "It surpasses even the wildest\l"
               "of imaginations…\p"
               "Here you are… your reward.\n"
               "That will come down to ¥25000")
        destroy_speech_bubble
        closemessage
        showmoneybox(0, 0, 0)
        delay(30)
        playse(SE_SHOP)
        addmoney(25000, 0)
        updatemoneybox(0, 0)
        delay(30)
        hidemoneybox
        load_speech_bubble(LOCALID_OLD_MAN)
        msgbox("Spend it wisely…\p"
               "Thanks again, good friend…")
        setflag(FLAG_MURENA_SHOWN_RELICANTH)
        destroy_speech_bubble
        closemessage
    } else {
        load_speech_bubble(LOCALID_OLD_MAN)
        msgbox("I see…\p"
               "What I'm looking for you do\n"
               "not possess…\p"
               "If you could find it for me…\n"
               "I shall be very grateful…")
        destroy_speech_bubble
        closemessage
    }
    release
    end
}

script MurenaCity_EventScript_Wingull {
    lock
    playmoncry(SPECIES_WINGULL, 0)
    waitmoncry
    release
    end
}

script MurenaCity_EventScript_MarketSalesWoman {
    giveitem(ITEM_TENTACOOL_SKEWER, 1)
    end
}

script MurenaCity_EventScript_MarketSalesMan {
    msgbox("If I sell anything?\n"
           "No no, talk to my wife for that.\l"
           "I just wanna grill for pete's sake.", MSGBOX_NPC)
    end
}

script MurenaCity_EventScript_RichBoy {
    msgbox("Tssk…\p"
           "You may've beat me at FIRWEALD,\n"
           "but once my daddy's boat arrives\l"
           "I'll show you who's boss.", MSGBOX_NPC)
    end
}

script MurenaCity_EventScript_Sailor1 {
    msgbox("The breeze here is so nice.\n"
           "There truly is no place like MURENA.", MSGBOX_NPC)
    end
}

script MurenaCity_EventScript_Sailor2 {
    msgbox("A WINGULL just took my fry…\p"
           "Why do we keep these pests\n"
           "around anyway?", MSGBOX_NPC)
    end
}

script MurenaCity_EventScript_Sailor3 {
    msgbox("You should try a TENTACOOL skewer!\n"
           "They truly do taste like heaven!", MSGBOX_NPC)
    end
}

script MurenaCity_EventScript_BugCatcher {
    msgbox("I wonder what BUG-types live\n"
           "across the EURUS region.", MSGBOX_NPC)
    end
}

script MurenaCity_EventScript_Woman {
    
}

script MurenaCity_EventScript_OldWoman {
    msgbox("Oh, how it's changed here.\n"
           "See, back in my day…", MSGBOX_NPC)
    delay(60)
    msgbox("(As you snap back to your senses\n"
           "you realize you zoned out…)")
    delay(24)
    msgbox("And that's why you young\n"
           "whippasnappas…", MSGBOX_NPC)
    playse(SE_PIN)
    applymovement(LOCALID_OLD_WOMAN, Common_Movement_ExclamationMark)
    waitmovement(0)
    msgbox("Hey, are you even listening to me?", MSGBOX_NPC)
    end       
}

script MurenaCity_EventScript_Fisherman {
    msgbox("I'm warming up.\p"
           "It's cold out at sea y'know.", MSGBOX_NPC)
    end
}

script MurenaCity_EventScript_LittleGirl {
    msgbox("When I grow up, I want to be\n"
           "a fisher. Just like daddy!", MSGBOX_NPC)
    end
}

script MurenaCity_EventScript_CitySign {
    msgbox("MURENA CITY\n"
           "“Fishing hotspot turned shipping port.”", MSGBOX_SIGN)
    end
}

script MurenaCity_EventScript_FishMarketSign {
    msgbox("MURENA FISHING MARKET\n"
           "“Get yer fish 'ere!”", MSGBOX_SIGN)
    end
}

script MurenaCity_EventScript_DockyardSign {
    msgbox("MURENA DOCKYARD", MSGBOX_SIGN)
    end
}

script MurenaCity_EventScript_WailordFishingClubSign {
    msgbox("WAILORD FISHING CLUB\n"
           "“In search of the oceanic giant!”", MSGBOX_SIGN)
    end
}

movement MurenaCity_Movement_Grunt2PunchFisherman {
    lock_facing_direction
    walk_slow_up
    unlock_facing_direction
    walk_in_place_fastest_down * 3
    walk_fast_down
}

movement MurenaCity_Movement_FishermanStrugglesBack {
    walk_in_place_fastest_up * 2
    lock_facing_direction
    walk_fast_down
    unlock_facing_direction
}

movement MurenaCity_Movement_FishermanRunToPlayer1 {
    walk_fast_right * 3
    walk_fast_up
    walk_fast_right
    walk_fast_up
    face_left
}

movement MurenaCity_Movement_FishermanRunToPlayer2 {
    walk_fast_right * 2
    walk_fast_up * 2
    walk_fast_right * 2
    walk_fast_down
    face_left
}

movement MurenaCity_Movement_PlayerWalkToGrunt {
    walk_left
    walk_down
    face_left
}

movement MurenaCity_Movement_Grunt1RunAway {
    walk_fast_up
    walk_fast_right * 2
    walk_fast_up * 6
}

movement MurenaCity_Movement_Grunt2RunAway {
    delay_16
    walk_fast_right * 3
    walk_fast_up * 6
}

movement MurenaCity_Movement_FishermanWalkToPlayer1 {
    walk_left * 2
    face_down
}

movement MurenaCity_Movement_FishermanWalkToPlayer2 {
    walk_left
    walk_up
    walk_left
    face_down
}

movement MurenaCity_Movement_FishermanWalkToShipyard {
    walk_left * 2
    walk_up
}

movement MurenaCity_Movement_SailToLitor {
    walk_left
    walk_fast_left * 3
    walk_left
    walk_down
    walk_fast_down * 2
    walk_fastest_down * 12
    walk_fast_down * 2
    walk_down
    walk_right
    walk_fast_right * 2
    walk_fastest_right * 7
    walk_fast_right * 2
    walk_right
    walk_up
    walk_fast_up * 4
    walk_up
    walk_right
    walk_fast_right * 2
    walk_fastest_right * 65
    walk_fast_right * 2
    walk_right
    walk_down
    walk_fast_down * 2
    walk_down
    walk_right
    walk_fast_right * 2
    walk_right
    walk_down
    walk_fast_down * 2
    walk_fastest_down * 12
    walk_fast_down * 2
    walk_down
    walk_right
    walk_fast_right * 2
    walk_fastest_right * 10
    walk_fast_right * 2
    walk_right
    walk_up
    walk_fast_up
    walk_up
    walk_right
    walk_fast_right * 2
    walk_fastest_right * 7
    walk_fast_right * 2
    walk_right
    walk_up
    walk_fast_up * 2
    walk_fastest_up * 2
    walk_fast_up * 2
    walk_up
    face_down
}

movement MurenaCity_Movement_ExitBoatLitorTown {
    walk_left * 2
    face_right
}

text MurenaCity_Text_GruntDefeat {
    "Well that backfired…"
}

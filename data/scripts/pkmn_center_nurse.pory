const LOCALID_NURSE = 1

script Common_EventScript_PkmnCenterNurse {
    special(CheckPokeCenterLocation)
    load_speech_bubble(LOCALID_NURSE)
    msgbox("Welcome to {STR_VAR_1}'s\n"
           "POKéMON CENTER.")
    destroy_speech_bubble
    closemessage
    load_speech_bubble(LOCALID_NURSE)
    msgbox("Would you like to rest your POKEMON?", MSGBOX_YESNO)
    destroy_speech_bubble
    switch (var(VAR_RESULT)) {
        case YES:
            incrementgamestat(GAME_STAT_USED_POKECENTER)
            msgbox("Okay, I'll take your POKéMON\n"
                   "for a few seconds.", MSGBOX_NPC)
            applymovement(LOCALID_NURSE, Common_Movement_WalkInPlaceFastestLeft)
            waitmovement(0)
            dofieldeffect(FLDEFF_POKECENTER_HEAL)
            waitfieldeffect(FLDEFF_POKECENTER_HEAL)
            applymovement(LOCALID_NURSE, Common_Movement_WalkInPlaceFastestDown)
            waitmovement(0)
            special(HealPlayerParty)
            if (flag(FLAG_POKERUS_EXPLAINED) == FALSE) {
                specialvar(VAR_RESULT, IsPokerusInParty)
                if(var(VAR_RESULT) == TRUE) {
                    msgbox("Your POKéMON may be infected with\n"
	                       "POKéRUS.\p"
	                       "Little is known about the POKéRUS\n"
	                       "except that they are microscopic life-\l"
	                       "forms that attach to POKéMON.\p"
	                       "While infected, POKéMON are said to\n"
	                       "grow exceptionally well.", MSGBOX_NPC)
                    setflag(FLAG_POKERUS_EXPLAINED)
                }
            } else {
                msgbox("Thank you for waiting.\p"
                       "We've restored your POKéMON\n"
                       "to full health.", MSGBOX_NPC)
                applymovement(LOCALID_NURSE, Common_Movement_NurseBow)
                waitmovement(0)
            }
            break
        case NO:
            break
    }
    msgbox("Thanks you for visiting\n"
           "{STR_VAR_1}'s POKEMON CENTER!", MSGBOX_NPC)
    end
}

script Common_EventScript_PkmnCenterTerminal {
    checkitem(ITEM_TERMINAL_PASS, 1)
    if (var(VAR_RESULT) == FALSE) {
        goto(Common_EventScript_PokemonCenterTerminal_NoTerminalPass)
    }
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkInPlaceFastestUp)
    msgbox("{PLAYER} swiped the TERMINAL PASS…")
    waitmovement(0)
    closemessage
    playfanfare(MUS_HEAL)
    waitfanfare
    special(HealPlayerParty)
    msgbox("Thank you for using {STR_VAR_1}'s\n"
           "POKéMON CENTER TERMINAL!")
    closemessage
    end
}

script Common_EventScript_PokemonCenterTerminal_NoTerminalPass {
    msgbox("You need a TERMINAL PASS to be able to\n"
           "operate the POKéMON CENTER TERMINAL…")
    closemessage
    end
}
